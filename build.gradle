plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.9'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'nebula.integtest' version '9.6.2'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version "3.4.0.2513"
}



group = 'ru.myskill'
version = '0.0.1'
sourceCompatibility = '17'



configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springCloudVersion', "2021.0.3")
    set('testcontainersVersion', "1.17.6")
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled(true)
    }
}

test.finalizedBy jacocoTestReport

tasks.named("sonarqube").configure {
    dependsOn test
}

sonarqube {
    properties {
        property "sonar.projectKey", "testinfra_blog_AYplqN242RPnQmk__2OM"
        property "sonar.organization", "skilljava"
    }
}

checkstyle {
    toolVersion = 10.1
    maxWarnings(1)
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor  "org.springframework.boot:spring-boot-configuration-processor"
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    implementation   'org.mapstruct:mapstruct:1.5.2.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testCompileOnly 'org.projectlombok:lombok'
    testRuntimeOnly 'org.postgresql:postgresql'

    integTestImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:2.1.3.RELEASE'
    integTestImplementation 'org.testcontainers:postgresql'
    integTestImplementation 'com.github.dasniko:testcontainers-keycloak:2.2.2'
    integTestImplementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    integTestCompileOnly 'org.projectlombok:lombok'
    integTestRuntimeOnly 'com.h2database:h2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


tasks.named('integrationTest') {
    useJUnitPlatform()
}